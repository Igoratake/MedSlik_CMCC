#!/bin/csh -f
#
#	$Id: nhlcc.csh,v 1.6 2010-04-02 17:52:38 haley Exp $
#

#*********************************************#
#                                             #
# Make sure NCARG_ROOT is set for this script #
#                                             #
#*********************************************#
setenv NCARG_ROOT  `ncargpath root`

if ($status != 0) then
	exit 1
endif

set xlibs = "-lX11 -lXext"
set cairolib = "-lcairo -lfontconfig -lpixman-1 -lfreetype -lexpat -lpng -lz"
set system   = ""LINUX""
set cc       = "gcc"
set defines  = "-DSYSV -D_POSIX_SOURCE -D_XOPEN_SOURCE -DByteSwapped -D__UNIXOS2__ -DNeedFuncProto"
set loadflags  = "-ansi -fPIC  -O0   "
set libdir   = `ncargpath lib`
set incdir   = `ncargpath include`
set syslibdir = ""
set sysincdir = ""
set ro       = "$libdir/ncarg/robj"
set f77libs  = "-lgfortran -lm"
set newargv = "$cc $defines $loadflags"
set libpath = "-L$libdir $syslibdir"
set incpath = "-I$incdir $sysincdir"

#
# set up default libraries
#
set libncarg    = "-lncarg"
set libgks      = "-lncarg_gks"
set libcgks     = "-lncarg_gks_cairo"
set libmath     = ""
set libncarg_c  = "-lncarg_c"
set libhlu      = "-lhlu"
set libchlu     = "-lhlu_cairo"
set ncarbd      = "$ro/libncarbd.o"
set ngmathbd    = "$ro/libngmathbd.o"
set extra_libs

set robjs
unset CAIRO_LD
unset NGMATH_LD
unset NGMATH_BLOCKD_LD

foreach arg ($argv)
  switch ($arg)

  case "-XmXt":
  case "-xmxt":
    set extra_libs = "$extra_libs -lXm -lXt -lSM -lICE"
    breaksw

  case "-cairo":
    set CAIRO_LD
    breaksw

  case "-ngmath":
    set libmath     = "-lngmath"
    breaksw

  case "-ncarbd":
    set robjs = "$robjs $ncarbd"
    set NGMATH_BLOCKD_LD
    breaksw

  case "-ngmathbd":
    set robjs = "$robjs $ngmathbd"
# Make sure the ngmath blockdata routine doesn't get loaded twice.
    unset NGMATH_BLOCKD_LD
    breaksw

  case "-netcdf":
  case "-cdf":
    set extra_libs = "$extra_libs -lnetcdf -lcurl -lcrypto -lssl -lrt -lhdf5_hl -lhdf5 -lsz -lz"
    breaksw

  case "-hdf":
    set extra_libs = "$extra_libs -lmfhdf -ldf -ljpeg -lz"
    breaksw

  default:
    set newargv = "$newargv $arg"
  endsw
end


#
# If -ncarbd was set, *and* the ngmath library was loaded,
# then automatically take care of loading libngmathbd.o.
#
if ($?NGMATH_LD && $?NGMATH_BLOCKD_LD) then
  set robjs = "$robjs $ngmathbd"
endif

if ($?CAIRO_LD) then
  set ncarg_libs = "$libchlu $libncarg $libcgks $libncarg_c $libmath"
  set newargv = "$newargv $libpath $incpath $extra_libs $robjs $ncarg_libs $xlibs $cairolib $f77libs"
else
  set ncarg_libs = "$libhlu $libncarg $libgks $libncarg_c $libmath"
  set newargv = "$newargv $libpath $incpath $extra_libs $robjs $ncarg_libs $xlibs $f77libs"
endif

echo $newargv
eval $newargv
